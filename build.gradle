plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.5'
}

group = 'me.chrommob'
version = '1.4.3'

repositories {
    mavenCentral()
    maven {
        url = uri("https://repo.opencollab.dev/main/")
    }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "sonatype-oss-snapshots1"
        url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        url 'https://repo.codemc.io/repository/maven-releases/'
    }
    maven {
        url 'https://repo.codemc.io/repository/maven-snapshots/'
    }
    maven {
        name = "aikar-repo"
        url = "https://repo.aikar.co/content/groups/aikar/"
    }
}

dependencies {
    compileOnly "org.geysermc.floodgate:api:2.2.3-SNAPSHOT"
    compileOnly "org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT"
    implementation "net.kyori:adventure-text-minimessage:4.18.0-SNAPSHOT"
    implementation "net.kyori:adventure-platform-bukkit:4.3.5-SNAPSHOT"
    implementation "com.github.retrooper:packetevents-spigot:2.7.1-SNAPSHOT"
    implementation "org.bstats:bstats-bukkit:3.1.0"
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    implementation "net.kyori:adventure-text-serializer-plain:4.18.0-SNAPSHOT"

}

shadowJar {
    relocate 'io.github.retrooper.packetevents', 'me.chrommob.baritoneremover.libs.io.packetevents'
    relocate 'com.github.retrooper.packetevents', 'me.chrommob.baritoneremover.libs.com.packetevents'
    relocate 'org.bstats', 'me.chrommob.baritoneremover.libs.bstats'
    relocate 'net.kyori', 'me.chrommob.baritoneremover.libs.kyori'
}

assemble.dependsOn(shadowJar)

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
